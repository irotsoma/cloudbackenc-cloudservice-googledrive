/*
 * Copyright (C) 2016  Irotsoma, LLC
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>
 */

buildscript {
    ext.kotlin_version = '1.0.3'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

group 'com.irotsoma.cloudbackenc.cloudservice'
version '0.1-SNAPSHOT'
def localMavenRepository = "$rootDir/../../maven-repository"
def secretFolder = "$rootDir/../secret"
apply plugin: 'java'
apply plugin: 'kotlin'

repositories {
    mavenCentral()
    maven{url localMavenRepository}
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.irotsoma.cloudbackenc:common:0.1-SNAPSHOT'
    compile 'com.irotsoma.cloudbackenc:cloudservice:0.1-SNAPSHOT'
    compile 'org.springframework:spring-web:4.3.2.RELEASE'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.8.1'
    compile 'com.google.apis:google-api-services-drive:v3-rev36-1.22.0'
    compile 'com.google.api-client:google-api-client-java6:1.22.0'
    compile 'com.google.oauth-client:google-oauth-client-jetty:1.22.0'

}

apply plugin: 'maven-publish'
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Google Drive cloud service implementation for CloudBackEnc.')
                root.appendNode('url', 'https://irotsoma.github.io/cloudbackenc/')
                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'GNU Lesser General Public License, Version 3.0')
                license.appendNode('url', 'http://www.gnu.org/licenses/lgpl.txt')
                license.appendNode('distribution', 'repo')
                def developers = root.appendNode('developers')
                def developer1 = developers.appendNode('developer')
                developer1.appendNode('id','irotsoma')
                developer1.appendNode('name', 'Justin Zak')
                developer1.appendNode('email','irotsomadev@gmail.com')
                developer1.appendNode('organization', 'Irotsoma, LLC')
                developer1.appendNode('organizationUrl','https://www.irotsoma.com')
                def scm = root.appendNode('scm')
                scm.appendNode('connection','scm:git:git@github.com:irotsoma/cloudbackenc.git')
                scm.appendNode('developerConnection','scm:git:git@github.com:irotsoma/cloudbackenc.git')
                scm.appendNode('url','git@github.com:irotsoma/cloudbackenc.git')
            }
        }
    }
    repositories {
        maven {
            url localMavenRepository
        }
    }
}
apply plugin: 'signing'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
signing {
    required { isReleaseVersion }
    sign configurations.archives
}
jar {
    //build a fat jar with dependencies to ease central controller loading resources at runtime
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    //but exclude anything that will already be loaded by the central controller to reduce the file size and chances of
    //version conflicts
    exclude 'kotlin/**', 'com/irotsoma/cloudbackenc/common/**', 'javax/**', 'com/fasterxml/jackson/**', 'META-INF/*/**'
}

sourceSets {
    main {
        java{
            srcDir 'src/main/kotlin'
        }
        resources {
            srcDir "$buildDir/license-reports/"
        }
    }
}

//this section downloads some reports regarding the licenses of various dependencies and includes them in the
// META-INF/licenses folder
apply plugin: 'com.github.hierynomus.license'
license {
    ignoreFailures true
    mapping ('kt', 'JAVADOC_STYLE')
    excludes(['**/*.json', '**/*.properties', '**/LICENSE', '**/*license*.html', '**/*license*.xml'])
}
task copyLicenseReports(type: Copy){
    from "$buildDir/reports/license/"
    into "$buildDir/license-reports/META-INF/licenses"
}
assemble.dependsOn 'downloadLicenses'
assemble.dependsOn 'copyLicenseReports'
copyLicenseReports.mustRunAfter 'downloadLicenses'


//This section will copy the GoogleDriveSettings.kt file from the /secret folder before build and then replace it with
//the original afterward.  This allows for putting the actual api key and secret in a separate repository and using it
//only at compile time. Adjust the variables at the top to change locations and file name.
def packageLocation = 'src/main/kotlin/com/irotsoma/cloudbackenc/cloudservice/googledrive/'
def fileName = 'GoogleDriveSettings.kt'
def tempFolder = 'build/tmp'

task copySettingsToTemp(type: Copy){
    from packageLocation
    include fileName
    into tempFolder
}
task copySecretToSource(type: Copy){
    from secretFolder
    include fileName
    into packageLocation
}
task copyBlankSettingsToSource(type: Copy){
    from tempFolder
    include fileName
    into packageLocation
}
assemble.dependsOn 'copySettingsToTemp'
assemble.dependsOn 'copySecretToSource'
copySecretToSource.mustRunAfter 'copySettingsToTemp'
build.dependsOn 'copyBlankSettingsToSource'
copyBlankSettingsToSource.mustRunAfter 'assemble'